@PACKAGE_INIT@

# Used build options values
set(OPENDAQ_LINK_3RD_PARTY_LIBS_STATICALY "@OPENDAQ_LINK_3RD_PARTY_LIBS_STATICALY@")
set(OPENDAQ_LINK_RUNTIME_STATICALLY "@OPENDAQ_LINK_RUNTIME_STATICALLY@")

# SDK identity variables
set(OPENDAQ_SDK_NAME "@OPENDAQ_SDK_NAME@")
set(OPENDAQ_SDK_TARGET_NAME "@OPENDAQ_SDK_TARGET_NAME@")
set(OPENDAQ_SDK_TARGET_NAMESPACE "@OPENDAQ_SDK_TARGET_NAMESPACE@")

list(APPEND CMAKE_MODULE_PATH 
     "@PACKAGE_CMAKE_INSTALL_DATAROOTDIR@"
     "@PACKAGE_CMAKE_INSTALL_DATAROOTDIR@/cmake"
)

list(APPEND CMAKE_PREFIX_PATH
    "@PACKAGE_INSTALLED_CMAKE_CONFIG_DIR@"
    "@PACKAGE_CMAKE_INSTALL_DATAROOTDIR@/cmake"
    "${PACKAGE_PREFIX_DIR}/CMake"
)

set(OPENDAQ_MODULE_SUFFIX ".module${CMAKE_SHARED_LIBRARY_SUFFIX}")
set(OPENDAQ_MODULES_DIR "@PACKAGE_INSTALLED_MODULES_DIR@/modules")
message(STATUS "openDAQ modules at: ${OPENDAQ_MODULES_DIR}")

include(CMakeFindDependencyMacro)

find_dependency(fmt REQUIRED)
find_dependency(tsl-ordered-map REQUIRED)
find_dependency(date REQUIRED)

find_package(xxHash QUIET)
if (NOT xxHash_FOUND)
    find_dependency(PkgConfig REQUIRED)
    pkg_check_modules(xxHash libxxhash REQUIRED IMPORTED_TARGET GLOBAL)
    add_library(xxHash::xxhash ALIAS PkgConfig::xxHash)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/openDAQ.cmake)

include(openDAQBaseUtils)
include(DeprecatedCommands)
